buildscript {
    repositories {
        maven { url("https://files.minecraftforge.net/maven") }
        maven { url("https://libraries.minecraft.net") }
        maven { url("https://plugins.gradle.org/m2/") }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath(
            group: "net.minecraftforge.gradle",
            name: "ForgeGradle",
            version: forge_gradle_version,
            changing: true
        )
        classpath(
            "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        )
        classpath(
            "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
        )
    }
}

apply(plugin: "net.minecraftforge.gradle")
apply(plugin: "kotlin")
apply(plugin: "kotlinx-serialization")

version = mod_version
group = "com.mairwunnx.$mod_id"
archivesBaseName = mod_name

configurations {
    embed
    compile.extendsFrom(embed)
}

minecraft {
    mappings(
        channel: forge_mappings_channel_type,
        version: forge_mappings_channel_version
    )

    runs {
        client {
            //noinspection GroovyAssignabilityCheck
            workingDirectory(project.file(dev_run_client_path))
            property("forge.logging.markers", "SCAN,REGISTRIES,REGISTRYDUMP")
            property("forge.logging.console.level", "debug")
            mods {
                RandomTeleport {
                    //noinspection GroovyAssignabilityCheck
                    source(sourceSets.main)
                }
            }
        }

        server {
            //noinspection GroovyAssignabilityCheck
            workingDirectory(project.file(dev_run_server_path))
            property("forge.logging.markers", "SCAN,REGISTRIES,REGISTRYDUMP")
            property("forge.logging.console.level", "debug")
            mods {
                RandomTeleport {
                    //noinspection GroovyAssignabilityCheck
                    source(sourceSets.main)
                }
            }
        }
    }
}

repositories {
    jcenter()
    mavenCentral()
    maven { url("https://minecraft.curseforge.com/api/maven/") }
    maven { url("https://libraries.minecraft.net") }
    maven { url("https://jitpack.io") }
}

dependencies {
    minecraft(group: "net.minecraftforge", name: "forge", version: forge_version)
    compile(group: "com.mojang", name: "brigadier", version: brigadier_version)
    compile(
        group: "org.jetbrains.kotlinx",
        name: "kotlinx-serialization-runtime",
        version: kotlinx_serialization_version
    )
    embed(
        group: "org.jetbrains.kotlinx",
        name: "kotlinx-serialization-runtime",
        version: kotlinx_serialization_version
    )
    compile(
        group: "org.jetbrains.kotlin",
        name: "kotlin-stdlib-$kotlin_jdk_version_target",
        version: kotlin_version
    )
    embed(
        group: "org.jetbrains.kotlin",
        name: "kotlin-stdlib-$kotlin_jdk_version_target",
        version: kotlin_version
    )
    compile(
        group: "com.github.projectessentials",
        name: "ProjectEssentials-Permissions",
        version: permissions_version
    )
    compile(
        group: "com.github.projectessentials",
        name: "ProjectEssentials-Cooldown",
        version: cooldown_version
    )
}

jar {
    from configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }
    manifest {
        attributes([
            "Specification-Title"     : mod_name,
            "Specification-Vendor"    : mod_version,
            "Specification-Version"   : mod_vendor,
            "Implementation-Title"    : mod_name,
            "Implementation-Version"  : mod_version,
            "Implementation-Vendor"   : mod_vendor,
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

sourceCompatibility = targetCompatibility =
    compileJava.sourceCompatibility =
        compileJava.targetCompatibility = project_jvm_version_target

compileKotlin.kotlinOptions.jvmTarget =
    compileTestKotlin.kotlinOptions.jvmTarget = project_jvm_version_target

//noinspection GroovyAssignabilityCheck
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        freeCompilerArgs += "-Xuse-experimental=kotlin.Experimental"
    }
}
